plugins {
    id "jaci.gradle.EmbeddedTools" version "2018.12.18" apply true
	id "java"
}

import jaci.gradle.toolchains.*
import jaci.gradle.nativedeps.*

// Change the line below if you change the name of your main Java class.
// Reminder: full declaration of name, i.e.: include packages
def mainClass = 'org.usfirst.frc.team1089.main.Main'

// Name of the files to create on the pi
def jarName = "vision"
def runScriptName = "runVision"
def killScriptName = "killVision"
def logName = "visionLog"

configurations {
    nativeLib {}
	config {}
}

dependencies {
    nativeLib fileTree(dir: 'lib/natives', include: '*.so')
	config fileTree(dir: 'config', include: '*.properties')
}

deploy {
    targets {
        target('rpi') {
			directory = 'output'
            failOnMissing = false
            timeout = 10
			
			locations {
				ssh {
					address = '10.10.89.20:5802'
					user = 'pi'
					password = 'raspberry'
				}
			}
        }
    }

    artifacts {
        // NOTE: Directory is relative to target directory
        javaArtifact('visionArtifact') {
            targets << 'rpi'
            filename = "${jarName}.jar"
            jar = 'jar'

            predeploy << {
                execute './${killScriptName}'
                execute 'rm -rf /home/pi/output'
            }

            postdeploy << {
                def cmd = "java -Djava.library.path=. -jar ${jarName}.jar"

                execute "echo \"${cmd}\" >> ${runScriptName}"
                execute "echo \"pkill -f \'${cmd}\'\" >> ${killScriptName}"
                execute "chmod 755 ${runScriptName}"
                execute "chmod 755 ${killScriptName}"
                execute "nohup ./${runScriptName} > /tmp/${logName} 2>&1 &"
            }
        }

        // Using a fileCollectionArtifact for deploying so's.
        // Bad practice, I think.

        fileCollectionArtifact('nativeCollectionArtifact') {
            dependsOn('visionArtifact')
            targets << 'rpi'
            files = project.configurations.nativeLib
        }

        fileCollectionArtifact('configCollectionArtifact') {
            targets << 'rpi'
            files = project.configurations.config
            dependsOn('visionArtifact')
        }
    }
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    // Set up manifest to point the main class to the right class.
    manifest {
        attributes 'Main-Class': mainClass
    }
}